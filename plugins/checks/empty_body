#!/usr/bin/perl
#
# Block mail which has an empty body.
#
#


use Qpsmtpd::Constants;


sub hook_data_post
{
    my ( $self, $transaction ) = (@_);


    #
    # If the mail is rejected terminate quickly
    #
    if ( ( $transaction->notes("reject") || 0 ) == 1 )
    {
        $self->log( LOGWARN,
                    $self->plugin_name() .
                      ": terminating as mail is already rejected"
                  );
        return DECLINED;
    }



    #
    # Get the domain this mail is for - this domain is setup
    # in the 'test_recipient' plugin
    #
    my $domain = $transaction->notes("domain") || undef;
    return DECLINED unless ( defined($domain) );



    #
    # If this checks isn't enabled then return immediately
    #
    if (
         !( ( -e "/srv/$domain/checks/empty_body" ) ||
            ( -e "/srv/$domain/checks/all" ) ) )
    {
        $self->log( LOGWARN,
                $self->plugin_name() . ": plugin disabled for domain $domain" );
        return DECLINED;
    }



    #
    #  Test the size of the body
    #
    if ( $transaction->body_size == 0 )
    {
        $transaction->notes( "reject",  1 );
        $transaction->notes( "blocker", $self->plugin_name() );
        $transaction->notes( "reason",  "Empty body" );
        return DECLINED;
    }

    #
    #  Terminate.
    #
    $self->log( LOGWARN,
         $self->plugin_name() . ": plugin passed - mail will not be rejected" );

    return DECLINED;
}




#
# End of plugin
#
1;
