#!/usr/bin/perl
#
# Block mail which should be DKIM signed, but which isn't.
#
#


use Qpsmtpd::Constants;


sub hook_data_post
{
    my ( $self, $transaction ) = (@_);


    #
    # If the mail is rejected terminate quickly
    #
    if ( ( $transaction->notes("reject") || 0 ) == 1 )
    {
        $self->log( LOGWARN,
                    $self->plugin_name() .
                      ": terminating as mail is already rejected"
                  );
        return DECLINED;
    }



    #
    # Get the domain this mail is for - this domain is setup
    # in the 'test_recipient' plugin
    #
    my $domain = $transaction->notes("DOMAIN") || undef;
    return DECLINED unless ( defined($domain) );



    #
    # If this check isn't enabled then return immediately
    #
    if (
         !( ( -e "/srv/$domain/checks/dkim" ) ||
            ( -e "/srv/$domain/checks/all" ) ) )
    {
        $self->log( LOGWARN,
                $self->plugin_name() . ": plugin disabled for domain $domain" );
        return DECLINED;
    }



    #
    #  These domains *always* sign with DKIM.
    #
    #  If the signature is missing that means their domains are being spoofed.
    #
    my @dkim_domains =
      qw ! amazon apple bankofamerica bofa ebay facebookemail gmail googlemail groupon itunes linkedin netflix paypal priceline quickbooks twitter yahoo !;

    #
    #  Get the sender address, setup in the connection-phase
    #
    my $from = $transaction->notes("MAIL-FROM");
    $from =~ s/^<|>$//g;
    $from = lc($from);

    #
    # Look for the domains we know should be signed.
    #
    # TODO: Look at dmarc?
    #
    foreach my $domain (@dkim_domains)
    {

        #
        #  If the sender matches one of our domain-key using domains
        # then we'll reject if there is no domainkey-signature present.
        #
        if ( $from =~ /\@\Q$domain\E\./i )
        {

            #
            #  Get the signing header, if present.
            #
            my $dkim = $transaction->header->get('DomainKey-Signature') ||
              $transaction->header->get('DKIM-Signature') ||
              undef;

            #
            #  If we don't get a signature then we'll dump the mail.
            #
            if ( !defined($dkim) )
            {
                $self->log( LOGWARN, "DKIM - no header from:$from" );

                $transaction->notes( "reject",  1 );
                $transaction->notes( "blocker", $self->plugin_name() );
                $transaction->notes( "reason",
                                  "Missing DKIM signature on mail from $from" );
                return DECLINED;

            }
        }
    }




    #
    #  Terminate.
    #
    $self->log( LOGWARN,
          $self->plugin_name() . ": plugin passed: mail will not be rejected" );

    return DECLINED;
}




#
# End of plugin
#
1;
