#!/usr/bin/perl
#
# Block mail contains a header matching a Mailman approval post.
#
#


use Qpsmtpd::Constants;


sub hook_data_post
{
    my ( $self, $transaction ) = (@_);


    #
    # If the mail is rejected terminate quickly
    #
    if ( ( $transaction->notes("reject") || 0 ) == 1 )
    {
        $self->log( LOGWARN,
                    $self->plugin_name() .
                      ": terminating as mail is already rejected"
                  );
        return DECLINED;
    }



    #
    # Get the domain this mail is for - this domain is setup
    # in the 'test_recipient' plugin
    #
    my $domain = $transaction->notes("DOMAIN") || undef;
    return DECLINED unless ( defined($domain) );



    #
    # If this check isn't enabled then return immediately
    #
    if (
         !( ( -e "/srv/$domain/checks/mailman" ) ||
            ( -e "/srv/$domain/checks/all" ) ) )
    {
        $self->log( LOGWARN,
                $self->plugin_name() . ": plugin disabled for domain $domain" );
        return DECLINED;
    }


    #
    #  Make sure we have a Subject header.
    #
    my $subject = $transaction->header->get('Subject') || undef;

    #
    #  If we don't have one, or it is empty then we'll
    # return now.
    #
    if ( !defined($subject) || !length($subject) )
    {
        return DECLINED;
    }

    #
    #  Remove control characters & lower-case
    #
    $subject = lc($subject);
    $subject =~ s/[\r\n\t]/ /g;
    $subject =~ s/\s+/ /g;

    $subject = "[empty]" if ( !defined($subject) );

    #
    # Now test to see if it is an approval post
    #
    if ( ( $subject =~ / post from ([^ ]+) requires approval/ ) ||
         ( ( $subject =~ /moderator request\(s\) waiting/ ) && ( $subject =~ /logcheck/i ) ) )
    {
        $self->log( LOGWARN, "mailman matched on subject $subject" );

        $transaction->notes( "reject",  1 );
        $transaction->notes( "blocker", $self->plugin_name() );
        $transaction->notes( "reason",  "mailman posts not accepted here" );
        return DECLINED;
    }


    #
    #  Terminate.
    #
    $self->log( LOGWARN,
         $self->plugin_name() . ": plugin passed subject:$subject - mail will not be rejected" );

    return DECLINED;
}




#
# End of plugin
#
1;
